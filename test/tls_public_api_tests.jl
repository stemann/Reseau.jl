using Test
using Reseau

# Far-future monotonic deadline: pending forever from the test's perspective,
# but far from typemax so saturating arithmetic never wraps it.
const _TLS_FAR_FUTURE_NS = typemax(Int64) ÷ 2

isdefined(@__MODULE__, :_RESEAU_TLS_TEST_UTILS_LOADED) || include("tls_test_utils.jl")

function _tls_public_read_record(conn::NC.Conn)::Tuple{Vector{UInt8}, Vector{UInt8}}
    header = Vector{UInt8}(undef, 5)
    read!(conn, header)
    payload_len = (Int(header[4]) << 8) | Int(header[5])
    payload = Vector{UInt8}(undef, payload_len)
    payload_len == 0 || read!(conn, payload)
    return header, payload
end

@testset "TLS public API" begin
        @testset "direct socket-address connect/listen passthroughs" begin
            if Sys.iswindows() && VERSION < v"1.11.0"
                # Julia 1.10 Windows CI hangs in this direct SocketAddr TLS path;
                # newer Windows and non-Windows jobs keep covering it.
                @test_skip false
            else
                IP.shutdown!()
                listener = nothing
                client = nothing
                server = nothing
                try
                    listener = TL.listen(NC.loopback_addr(0), _tls_server_config(); backlog = 8)
                    laddr = TL.addr(listener)::NC.SocketAddrV4
                    accept_task = errormonitor(Threads.@spawn begin
                        conn = TL.accept(listener)
                        TL.handshake!(conn)
                        return conn
                    end)
                    client = TL.connect(
                        NC.loopback_addr(Int(laddr.port)),
                        NC.loopback_addr(0);
                        verify_peer = false,
                        server_name = "localhost",
                        handshake_timeout_ns = 1_000_000_000,
                    )
                    _tls_wait_task_done(accept_task)
                    server = fetch(accept_task)
                    client_local = TL.local_addr(client)::NC.SocketAddrV4
                    @test client_local.ip == NC.loopback_addr(0).ip
                    payload = UInt8[0x64, 0x69, 0x72]
                    recv_buf = Vector{UInt8}(undef, length(payload))
                    @test write(client, payload) == length(payload)
                    @test read!(server, recv_buf) === recv_buf
                    @test recv_buf == payload
                finally
                    _tls_close_quiet!(server)
                    _tls_close_quiet!(client)
                    _tls_close_quiet!(listener)
                    IP.shutdown!()
                end
            end
        end
        @testset "server client-auth runtime path" begin
            IP.shutdown!()
            listener = nothing
            accept_task = nothing
            client = nothing
            try
                request_listener = nothing
                request_accept = nothing
                request_client = nothing
                request_server = nothing
                request_server_cfg = TL.Config(
                    cert_file = _TLS_CERT_PATH,
                    key_file = _TLS_KEY_PATH,
                    client_auth = TL.ClientAuthMode.RequestClientCert,
                    verify_peer = false,
                )
                try
                    request_listener = TL.listen("tcp", "127.0.0.1:0", request_server_cfg; backlog = 8)
                    request_addr = TL.addr(request_listener)::NC.SocketAddrV4
                    request_accept = errormonitor(Threads.@spawn begin
                        conn = TL.accept(request_listener)
                        try
                            TL.handshake!(conn)
                            return conn
                        catch err
                            _tls_close_quiet!(conn)
                            return err
                        end
                    end)
                    request_client = _tls_connect("tcp", "127.0.0.1:$(Int(request_addr.port))", TL.Config(
                        verify_peer = false,
                        server_name = "localhost",
                    ))
                    @test request_client isa TL.Conn
                    _tls_wait_task_done(request_accept)
                    request_server = fetch(request_accept)
                    @test request_server isa TL.Conn
                finally
                    _tls_close_quiet!(request_server)
                    _tls_close_quiet!(request_client)
                    _tls_close_quiet!(request_listener)
                end
                strict_server_cfg = TL.Config(
                    cert_file = _TLS_CERT_PATH,
                    key_file = _TLS_KEY_PATH,
                    client_auth = TL.ClientAuthMode.RequireAndVerifyClientCert,
                    client_ca_file = _TLS_CERT_PATH,
                )
                listener = TL.listen("tcp", "127.0.0.1:0", strict_server_cfg; backlog = 8)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    conn = TL.accept(listener)
                    try
                        TL.handshake!(conn)
                        _tls_close_quiet!(conn)
                        return :ok
                    catch err
                        _tls_close_quiet!(conn)
                        return err
                    end
                end)
                client_cfg = TL.Config(verify_peer = false, server_name = "localhost", handshake_timeout_ns = 10_000_000_000)
                connect_err = nothing
                try
                    client = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", client_cfg)
                catch ex
                    connect_err = ex
                end
                if connect_err !== nothing
                    @test _tls_handshake_connect_error(connect_err)
                else
                    @test client isa TL.Conn
                end
                accept_task !== nothing && _tls_wait_task_done(accept_task)
            finally
                _tls_close_quiet!(client)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "ALPN negotiates on server and client paths" begin
            IP.shutdown!()
            listener = nothing
            client = nothing
            server = nothing
            try
                server_cfg = TL.Config(
                    verify_peer = false,
                    cert_file = _TLS_CERT_PATH,
                    key_file = _TLS_KEY_PATH,
                    alpn_protocols = ["h2", "http/1.1"],
                )
                listener = TL.listen("tcp", "127.0.0.1:0", server_cfg; backlog = 8)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    conn = TL.accept(listener)
                    TL.handshake!(conn)
                    return conn
                end)
                client_cfg = TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                    alpn_protocols = ["h2", "http/1.1"],
                )
                client = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", client_cfg)
                _tls_wait_task_done(accept_task)
                server = fetch(accept_task)
                @test TL.connection_state(client).alpn_protocol == "h2"
                @test TL.connection_state(server).alpn_protocol == "h2"
            finally
                _tls_close_quiet!(server)
                _tls_close_quiet!(client)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "ALPN h2 servers still accept http/1.1 clients without ALPN overlap" begin
            IP.shutdown!()
            listener = nothing
            client = nothing
            server = nothing
            try
                server_cfg = TL.Config(
                    verify_peer = false,
                    cert_file = _TLS_CERT_PATH,
                    key_file = _TLS_KEY_PATH,
                    alpn_protocols = ["h2"],
                )
                listener = TL.listen("tcp", "127.0.0.1:0", server_cfg; backlog = 8)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    conn = TL.accept(listener)
                    TL.handshake!(conn)
                    return conn
                end)
                client_cfg = TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                    alpn_protocols = ["http/1.1"],
                )
                client = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", client_cfg)
                _tls_wait_task_done(accept_task)
                server = fetch(accept_task)
                @test TL.connection_state(client).alpn_protocol == ""
                @test TL.connection_state(server).alpn_protocol == ""
            finally
                _tls_close_quiet!(server)
                _tls_close_quiet!(client)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "ALPN no-overlap fails the handshake" begin
            IP.shutdown!()
            listener = nothing
            client = nothing
            try
                server_cfg = TL.Config(
                    verify_peer = false,
                    cert_file = _TLS_CERT_PATH,
                    key_file = _TLS_KEY_PATH,
                    alpn_protocols = ["h2"],
                )
                listener = TL.listen("tcp", "127.0.0.1:0", server_cfg; backlog = 8)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    try
                        conn = TL.accept(listener)
                        TL.handshake!(conn)
                    catch ex
                        ex
                    end
                end)
                client_cfg = TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                    alpn_protocols = ["spdy/3"],
                )
                err = try
                    _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", client_cfg)
                    nothing
                catch ex
                    ex
                end
                @test err isa TL.TLSError
                if err isa TL.TLSError
                    @test occursin("alert 120", err.message)
                end
                _tls_wait_task_done(accept_task)
                server_err = fetch(accept_task)
                @test server_err isa TL.TLSError
            finally
                _tls_close_quiet!(client)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "show methods summarize TLS endpoints and handshake state" begin
            IP.shutdown!()
            tls_listener = nothing
            tcp_listener = nothing
            client_tcp = nothing
            server_tcp = nothing
            client_tls = nothing
            server_tls = nothing
            try
                listener_cfg = _tls_server_config()
                tls_listener = TL.listen("tcp", "127.0.0.1:0", listener_cfg; backlog = 8)
                tls_laddr = TL.addr(tls_listener)
                @test repr(tls_listener) == "TLS.Listener($(repr(tls_laddr)), active)"
                close(tls_listener)
                @test repr(tls_listener) == "TLS.Listener($(repr(tls_laddr)), closed)"
                tls_listener = nothing

                tcp_listener = ND.listen("tcp", "127.0.0.1:0"; backlog = 8)
                tcp_laddr = NC.addr(tcp_listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn NC.accept(tcp_listener))
                client_tcp = ND.connect("tcp", "127.0.0.1:$(Int(tcp_laddr.port))")
                _tls_wait_task_done(accept_task)
                server_tcp = fetch(accept_task)

                server_cfg = TL.Config(
                    verify_peer = false,
                    cert_file = _TLS_CERT_PATH,
                    key_file = _TLS_KEY_PATH,
                    alpn_protocols = ["h2", "http/1.1"],
                )
                client_cfg = TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                    alpn_protocols = ["h2", "http/1.1"],
                )
                client_tls = TL.client(client_tcp, client_cfg)
                server_tls = TL.server(server_tcp, server_cfg)

                client_local = TL.local_addr(client_tls)
                client_remote = TL.remote_addr(client_tls)
                server_local = TL.local_addr(server_tls)
                server_remote = TL.remote_addr(server_tls)

                @test repr(client_tls) == "TLS.Conn($(repr(client_local)) => $(repr(client_remote)), client, handshake pending)"
                @test repr(server_tls) == "TLS.Conn($(repr(server_local)) => $(repr(server_remote)), server, handshake pending)"

                server_task = errormonitor(Threads.@spawn TL.handshake!(server_tls))
                TL.handshake!(client_tls)
                _tls_wait_task_done(server_task)
                fetch(server_task)

                client_state = TL.connection_state(client_tls)
                server_state = TL.connection_state(server_tls)
                @test client_state.alpn_protocol == "h2"
                @test server_state.alpn_protocol == "h2"
                @test repr(client_tls) == "TLS.Conn($(repr(client_local)) => $(repr(client_remote)), client, $(client_state.version), $(client_state.alpn_protocol))"
                @test repr(server_tls) == "TLS.Conn($(repr(server_local)) => $(repr(server_remote)), server, $(server_state.version), $(server_state.alpn_protocol))"

                close(client_tls)
                close(server_tls)

                @test repr(client_tls) == "TLS.Conn($(repr(client_local)) => $(repr(client_remote)), client, closed)"
                @test repr(server_tls) == "TLS.Conn($(repr(server_local)) => $(repr(server_remote)), server, closed)"
            finally
                _tls_close_quiet!(server_tls)
                _tls_close_quiet!(client_tls)
                _tls_close_quiet!(server_tcp)
                _tls_close_quiet!(client_tcp)
                _tls_close_quiet!(tcp_listener)
                _tls_close_quiet!(tls_listener)
                IP.shutdown!()
            end
        end
        @testset "listener deadline, open state, and local_addr alias" begin
            IP.shutdown!()
            listener = nothing
            client = nothing
            server = nothing
            try
                listener = TL.listen("tcp", "127.0.0.1:0", _tls_server_config(); backlog = 8)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                @test isopen(listener)
                @test TL.local_addr(listener) == laddr

                TL.set_deadline!(listener, Int64(1))
                @test_throws TL.DeadlineExceededError TL.accept(listener)

                TL.set_deadline!(listener, Int64(0))
                accept_task = errormonitor(Threads.@spawn TL.accept(listener))
                client = NC.connect(NC.loopback_addr(Int(laddr.port)))
                _tls_wait_task_done(accept_task)
                server = fetch(accept_task)
                @test server isa TL.Conn

                @test close(listener) === nothing
                @test !isopen(listener)
                @test close(listener) === nothing
            finally
                _tls_close_quiet!(server)
                _tls_close_quiet!(client)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "connect/listen handshake and roundtrip" begin
            IP.shutdown!()
            listener = nothing
            client = nothing
            server = nothing
            try
                listener = TL.listen("tcp", "127.0.0.1:0", _tls_server_config(); backlog = 16)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    try
                        conn = TL.accept(listener)
                        TL.handshake!(conn)
                        buf = Vector{UInt8}(undef, 4)
                        read!(conn, buf)
                        write(conn, buf)
                        view_backing = fill(UInt8(0x00), 5)
                        view_buf = @view view_backing[2:4]
                        read!(conn, view_buf)
                        write(conn, view_buf)
                        return conn
                    catch err
                        return err
                    end
                end)
                client_cfg = TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                    handshake_timeout_ns = 10_000_000_000,
                )
                client = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", client_cfg)
                payload = UInt8[0x61, 0x62, 0x63, 0x64]
                @test write(client, payload) == 4
                recv_buf = Vector{UInt8}(undef, 4)
                @test read!(client, recv_buf) === recv_buf
                @test recv_buf == payload
                payload_view = @view payload[2:4]
                @test write(client, payload_view) == length(payload_view)
                recv_view_buf = Vector{UInt8}(undef, length(payload_view))
                @test read!(client, recv_view_buf) === recv_view_buf
                @test recv_view_buf == collect(payload_view)
                _tls_wait_task_done(accept_task)
                server_result = fetch(accept_task)
                server_result isa Exception && throw(server_result)
                server = server_result::TL.Conn
                state = TL.connection_state(client)
                @test state.handshake_complete
                @test state.version == "TLSv1.3"
                @test state.using_native_tls13
                server_state = TL.connection_state(server)
                @test server_state.handshake_complete
                @test server_state.version == "TLSv1.3"
                @test server_state.using_native_tls13
            finally
                _tls_close_quiet!(server)
                _tls_close_quiet!(client)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "zero-length I/O drives the handshake without application data" begin
            IP.shutdown!()
            listener = nothing
            client_tcp = nothing
            server_tcp = nothing
            client_tls = nothing
            server_tls = nothing
            server_task = nothing
            try
                listener = ND.listen("tcp", "127.0.0.1:0"; backlog = 8)
                laddr = NC.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn NC.accept(listener))
                client_tcp = ND.connect("tcp", "127.0.0.1:$(Int(laddr.port))")
                _tls_wait_task_done(accept_task)
                server_tcp = fetch(accept_task)
                client_tls = TL.client(client_tcp, TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                    handshake_timeout_ns = 2_000_000_000,
                ))
                server_tls = TL.server(server_tcp, _tls_server_config(handshake_timeout_ns = 2_000_000_000))

                server_task = errormonitor(Threads.@spawn read(server_tls, 0))
                @test write(client_tls, UInt8[]) == 0
                _tls_wait_task_done(server_task)
                @test fetch(server_task) == UInt8[]
                @test TL.connection_state(client_tls).handshake_complete
                @test TL.connection_state(server_tls).handshake_complete
                @test TL._pending_plaintext(client_tls) == 0
                @test TL._pending_plaintext(server_tls) == 0

                empty_buf = UInt8[]
                @test read!(client_tls, empty_buf) === empty_buf
                @test readbytes!(client_tls, empty_buf, 0) == 0
                @test Base.unsafe_read(client_tls, Ptr{UInt8}(C_NULL), UInt(0)) === nothing
            finally
                _tls_close_quiet!(server_tls)
                _tls_close_quiet!(client_tls)
                _tls_close_quiet!(server_tcp)
                _tls_close_quiet!(client_tcp)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end

            empty_operations = (
                ("read", conn -> read(conn, 0)),
                ("read!", conn -> read!(conn, UInt8[])),
                ("readbytes!", conn -> readbytes!(conn, UInt8[], 0)),
                ("unsafe_read", conn -> Base.unsafe_read(conn, Ptr{UInt8}(C_NULL), UInt(0))),
                ("write", conn -> write(conn, UInt8[])),
            )
            for (label, operation) in empty_operations
                @testset "$label surfaces handshake failure" begin
                    IP.shutdown!()
                    listener = nothing
                    client_tcp = nothing
                    peer_tcp = nothing
                    client_tls = nothing
                    try
                        listener = ND.listen("tcp", "127.0.0.1:0"; backlog = 1)
                        laddr = NC.addr(listener)::NC.SocketAddrV4
                        accept_task = errormonitor(Threads.@spawn NC.accept(listener))
                        client_tcp = ND.connect("tcp", "127.0.0.1:$(Int(laddr.port))")
                        _tls_wait_task_done(accept_task)
                        peer_tcp = fetch(accept_task)
                        close(peer_tcp)
                        peer_tcp = nothing
                        client_tls = TL.client(client_tcp, TL.Config(
                            verify_peer = false,
                            server_name = "localhost",
                            handshake_timeout_ns = 1_000_000_000,
                        ))
                        err = try
                            operation(client_tls)
                            nothing
                        catch ex
                            ex
                        end
                        @test err isa TL.TLSError || err isa EOFError
                        if err isa TL.TLSError
                            # A clean peer close must surface as EOFError, so a
                            # TLSError here must never be the catch-all wrapping
                            # a swallowed EOFError. Transport errors are still
                            # legitimate: Linux/Windows reset the connection in
                            # this race where macOS delivers a clean FIN.
                            @test !(err.cause isa EOFError)
                        end
                        @test !TL.connection_state(client_tls).handshake_complete
                    finally
                        _tls_close_quiet!(client_tls)
                        _tls_close_quiet!(peer_tcp)
                        _tls_close_quiet!(client_tcp)
                        _tls_close_quiet!(listener)
                        IP.shutdown!()
                    end
                end
            end
        end
        @testset "truncated TLS records surface unexpected EOF" begin
            @testset "during handshake" begin
                IP.shutdown!()
                listener = nothing
                client_tcp = nothing
                server_tcp = nothing
                server_tls = nothing
                try
                    listener = ND.listen("tcp", "127.0.0.1:0"; backlog = 1)
                    laddr = NC.addr(listener)::NC.SocketAddrV4
                    accept_task = errormonitor(Threads.@spawn NC.accept(listener))
                    client_tcp = ND.connect("tcp", "127.0.0.1:$(Int(laddr.port))")
                    _tls_wait_task_done(accept_task)
                    server_tcp = fetch(accept_task)
                    server_tls = TL.server(server_tcp, _tls_server_config(handshake_timeout_ns = 2_000_000_000))
                    write(client_tcp, UInt8[TL._TLS_RECORD_TYPE_HANDSHAKE, 0x03])
                    close(client_tcp)
                    client_tcp = nothing
                    err = try
                        TL.handshake!(server_tls)
                        nothing
                    catch ex
                        ex
                    end
                    @test err isa TL.TLSError
                    if err isa TL.TLSError
                        @test err.op == "handshake"
                        @test err.message == "unexpected EOF"
                        @test err.cause isa TL._TLSUnexpectedEOFError
                    end
                finally
                    _tls_close_quiet!(server_tls)
                    _tls_close_quiet!(server_tcp)
                    _tls_close_quiet!(client_tcp)
                    _tls_close_quiet!(listener)
                    IP.shutdown!()
                end
            end

            @testset "after handshake" begin
                IP.shutdown!()
                listener = nothing
                client_tcp = nothing
                server_tcp = nothing
                client_tls = nothing
                server_tls = nothing
                server_task = nothing
                try
                    listener = ND.listen("tcp", "127.0.0.1:0"; backlog = 1)
                    laddr = NC.addr(listener)::NC.SocketAddrV4
                    accept_task = errormonitor(Threads.@spawn NC.accept(listener))
                    client_tcp = ND.connect("tcp", "127.0.0.1:$(Int(laddr.port))")
                    _tls_wait_task_done(accept_task)
                    server_tcp = fetch(accept_task)
                    client_tls = TL.client(client_tcp, TL.Config(
                        verify_peer = false,
                        server_name = "localhost",
                        handshake_timeout_ns = 2_000_000_000,
                        min_version = TL.TLS1_2_VERSION,
                        max_version = TL.TLS1_2_VERSION,
                    ))
                    server_tls = TL.server(server_tcp, _tls_server_config(
                        handshake_timeout_ns = 2_000_000_000,
                        min_version = TL.TLS1_2_VERSION,
                        max_version = TL.TLS1_2_VERSION,
                    ))
                    server_task = errormonitor(Threads.@spawn TL.handshake!(server_tls))
                    TL.handshake!(client_tls)
                    _tls_wait_task_done(server_task)
                    fetch(server_task)

                    write(client_tls.tcp, UInt8[TL._TLS_RECORD_TYPE_APPLICATION_DATA, 0x03])
                    close(client_tls.tcp)
                    err = try
                        read(server_tls, 1)
                        nothing
                    catch ex
                        ex
                    end
                    @test err isa TL.TLSError
                    if err isa TL.TLSError
                        @test err.op == "read"
                        @test err.message == "unexpected EOF"
                        @test err.cause isa TL._TLSUnexpectedEOFError
                    end
                finally
                    _tls_close_quiet!(server_tls)
                    _tls_close_quiet!(client_tls)
                    _tls_close_quiet!(server_tcp)
                    _tls_close_quiet!(client_tcp)
                    _tls_close_quiet!(listener)
                    IP.shutdown!()
                end
            end
        end
        @testset "non-TLS first record surfaces a header error without an alert" begin
            IP.shutdown!()
            listener = nothing
            client_tcp = nothing
            server_tcp = nothing
            server_tls = nothing
            try
                listener = ND.listen("tcp", "127.0.0.1:0"; backlog = 1)
                laddr = NC.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn NC.accept(listener))
                client_tcp = ND.connect("tcp", "127.0.0.1:$(Int(laddr.port))")
                _tls_wait_task_done(accept_task)
                server_tcp = fetch(accept_task)
                server_tls = TL.server(server_tcp, _tls_server_config(handshake_timeout_ns = 2_000_000_000))
                write(client_tcp, UInt8[TL._TLS_RECORD_TYPE_APPLICATION_DATA, 0x03, 0x03, 0x00, 0x00])
                err = try
                    TL.handshake!(server_tls)
                    nothing
                catch ex
                    ex
                end
                @test err isa TL.TLSError
                if err isa TL.TLSError
                    @test err.op == "handshake"
                    @test err.message == "tls: first record does not look like a TLS handshake"
                    @test err.cause isa TL._TLSRecordHeaderError
                end

                # Already expired: enters the timeout branch without waiting.
                NC.set_read_deadline!(client_tcp, Int64(1))
                @test_throws NC.DeadlineExceededError read!(client_tcp, Vector{UInt8}(undef, 1))
            finally
                _tls_close_quiet!(server_tls)
                _tls_close_quiet!(server_tcp)
                _tls_close_quiet!(client_tcp)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "mixed-version failures retain the selected TLS 1.2 record state" begin
            @testset "server rejects inappropriate TLS fallback signaling" begin
                IP.shutdown!()
                listener = nothing
                client_tcp = nothing
                server_task = nothing
                try
                    listener = NC.listen(NC.loopback_addr(0); backlog = 1)
                    addr = NC.addr(listener)::NC.SocketAddrV4
                    server_task = errormonitor(Threads.@spawn begin
                        server_tcp = NC.accept(listener)
                        server_tls = TL.server(server_tcp, _tls_server_config(
                            handshake_timeout_ns = 2_000_000_000,
                        ))
                        try
                            TL.handshake!(server_tls)
                            return nothing
                        catch ex
                            return ex
                        finally
                            _tls_close_quiet!(server_tls)
                        end
                    end)

                    client_tcp = NC.connect(addr)
                    hello = TL._tls_auto_client_hello(TL.Config(
                        verify_peer = false,
                        server_name = "localhost",
                    ))
                    hello.supported_versions = UInt16[TL.TLS1_2_VERSION]
                    push!(hello.cipher_suites, TL._TLS_FALLBACK_SCSV)
                    TL._tls_write_tls_plaintext!(
                        client_tcp,
                        TL._TLS_RECORD_TYPE_HANDSHAKE,
                        TL._marshal_client_hello(hello),
                        TL._TLS_LEGACY_RECORD_VERSION,
                    )
                    header, payload = _tls_public_read_record(client_tcp)

                    _tls_wait_task_done(server_task)
                    err = fetch(server_task)
                    @test err isa TL.TLSError
                    if err isa TL.TLSError
                        @test err.cause isa TL._TLSAlertError
                        if err.cause isa TL._TLSAlertError
                            @test (err.cause::TL._TLSAlertError).alert == TL._TLS_ALERT_INAPPROPRIATE_FALLBACK
                        end
                    end
                    @test header[1] == TL._TLS_RECORD_TYPE_ALERT
                    @test payload == UInt8[
                        TL._TLS_ALERT_LEVEL_FATAL,
                        TL._TLS_ALERT_INAPPROPRIATE_FALLBACK,
                    ]

                    tls12_only = _tls_server_config(
                        min_version = TL.TLS1_2_VERSION,
                        max_version = TL.TLS1_2_VERSION,
                    )
                    @test TL._tls_check_inappropriate_fallback!(
                        tls12_only,
                        hello,
                        TL.TLS1_2_VERSION,
                    ) === nothing
                finally
                    _tls_close_quiet!(client_tcp)
                    _tls_close_quiet!(listener)
                    server_task isa Task && !istaskdone(server_task) && wait(server_task)
                    IP.shutdown!()
                end
            end

            @testset "client pre-CCS failure sends a plaintext alert from TLS 1.2 state" begin
                IP.shutdown!()
                listener = nothing
                client_tls = nothing
                server_task = nothing
                try
                    listener = NC.listen(NC.loopback_addr(0); backlog = 1)
                    addr = NC.addr(listener)::NC.SocketAddrV4
                    server_task = errormonitor(Threads.@spawn begin
                        server_tcp = NC.accept(listener)
                        try
                            record = UInt8[]
                            TL._tls_read_wire_record!(
                                server_tcp,
                                record,
                                TL._TLS12_MAX_CIPHERTEXT,
                                UInt16(0),
                            )
                            client_hello = TL._unmarshal_client_hello(copy(@view record[6:end]))
                            client_hello === nothing && error("failed to parse mixed ClientHello")
                            server_hello = TL._ServerHelloMsg()
                            server_hello.vers = TL.TLS1_2_VERSION
                            server_hello.random = fill(UInt8(0x44), 32)
                            server_hello.session_id = copy(client_hello.session_id)
                            server_hello.cipher_suite = UInt16(0xffff)
                            raw_server_hello = TL._marshal_server_hello(server_hello)
                            TL._tls_write_tls_plaintext!(
                                server_tcp,
                                TL._TLS_RECORD_TYPE_HANDSHAKE,
                                raw_server_hello,
                                TL.TLS1_2_VERSION,
                            )
                            return _tls_public_read_record(server_tcp)
                        finally
                            _tls_close_quiet!(server_tcp)
                        end
                    end)

                    client_tcp = NC.connect(addr)
                    client_tls = TL.client(client_tcp, TL.Config(
                        verify_peer = false,
                        server_name = "localhost",
                        handshake_timeout_ns = 2_000_000_000,
                    ))
                    displaced_state = TL._native_tls13_state(client_tls)
                    err = try
                        TL.handshake!(client_tls)
                        nothing
                    catch ex
                        ex
                    end
                    @test err isa TL.TLSError
                    if err isa TL.TLSError
                        @test err.cause isa TL._TLSAlertError
                        if err.cause isa TL._TLSAlertError
                            @test (err.cause::TL._TLSAlertError).alert == TL._TLS_ALERT_HANDSHAKE_FAILURE
                        end
                    end
                    @test client_tls.native_state isa TL._TLS12NativeState
                    @test displaced_state.version == UInt16(0)
                    @test isempty(displaced_state.record_buffer)

                    _tls_wait_task_done(server_task)
                    header, payload = fetch(server_task)
                    @test header[1] == TL._TLS_RECORD_TYPE_ALERT
                    @test header[2:3] == UInt8[0x03, 0x03]
                    @test payload == UInt8[TL._TLS_ALERT_LEVEL_FATAL, TL._TLS_ALERT_HANDSHAKE_FAILURE]
                finally
                    _tls_close_quiet!(client_tls)
                    _tls_close_quiet!(listener)
                    server_task isa Task && !istaskdone(server_task) && wait(server_task)
                    IP.shutdown!()
                end
            end

            @testset "server pre-CCS failure sends a plaintext alert from TLS 1.2 state" begin
                IP.shutdown!()
                listener = nothing
                client_tcp = nothing
                server_tls_ref = Ref{Union{Nothing, TL.Conn}}(nothing)
                displaced_ref = Ref{Union{Nothing, TL._TLS13NativeClientState}}(nothing)
                server_task = nothing
                try
                    listener = NC.listen(NC.loopback_addr(0); backlog = 1)
                    addr = NC.addr(listener)::NC.SocketAddrV4
                    server_task = errormonitor(Threads.@spawn begin
                        server_tcp = NC.accept(listener)
                        server_tls = TL.server(server_tcp, _tls_server_config(
                            handshake_timeout_ns = 2_000_000_000,
                        ))
                        server_tls_ref[] = server_tls
                        displaced_ref[] = TL._native_tls13_state(server_tls)
                        try
                            TL.handshake!(server_tls)
                            return nothing
                        catch ex
                            return ex
                        end
                    end)

                    client_tcp = NC.connect(addr)
                    hello = TL._tls_auto_client_hello(TL.Config(
                        verify_peer = false,
                        server_name = "localhost",
                    ))
                    hello.supported_versions = UInt16[TL.TLS1_2_VERSION]
                    hello.compression_methods = UInt8[0x01]
                    TL._tls_write_tls_plaintext!(
                        client_tcp,
                        TL._TLS_RECORD_TYPE_HANDSHAKE,
                        TL._marshal_client_hello(hello),
                        TL._TLS_LEGACY_RECORD_VERSION,
                    )
                    header, payload = _tls_public_read_record(client_tcp)

                    _tls_wait_task_done(server_task)
                    err = fetch(server_task)
                    @test err isa TL.TLSError
                    if err isa TL.TLSError
                        @test err.cause isa TL._TLSAlertError
                        if err.cause isa TL._TLSAlertError
                            @test (err.cause::TL._TLSAlertError).alert == TL._TLS_ALERT_ILLEGAL_PARAMETER
                        end
                    end
                    server_tls = server_tls_ref[]
                    @test server_tls isa TL.Conn
                    if server_tls isa TL.Conn
                        @test server_tls.native_state isa TL._TLS12NativeState
                    end
                    displaced_state = displaced_ref[]
                    @test displaced_state isa TL._TLS13NativeClientState
                    if displaced_state isa TL._TLS13NativeClientState
                        @test displaced_state.version == UInt16(0)
                        @test isempty(displaced_state.record_buffer)
                    end
                    @test header[1] == TL._TLS_RECORD_TYPE_ALERT
                    @test header[2:3] == UInt8[0x03, 0x03]
                    @test payload == UInt8[TL._TLS_ALERT_LEVEL_FATAL, TL._TLS_ALERT_ILLEGAL_PARAMETER]
                finally
                    _tls_close_quiet!(server_tls_ref[])
                    _tls_close_quiet!(client_tcp)
                    _tls_close_quiet!(listener)
                    server_task isa Task && !istaskdone(server_task) && wait(server_task)
                    IP.shutdown!()
                end
            end

            @testset "post-CCS fatal alert uses the installed TLS 1.2 write cipher" begin
                IP.shutdown!()
                listener = nothing
                client_tls = nothing
                server_tcp = nothing
                peer_state = TL._TLS12NativeState()
                try
                    listener = NC.listen(NC.loopback_addr(0); backlog = 1)
                    addr = NC.addr(listener)::NC.SocketAddrV4
                    accept_task = errormonitor(Threads.@spawn NC.accept(listener))
                    client_tcp = NC.connect(addr)
                    _tls_wait_task_done(accept_task)
                    server_tcp = fetch(accept_task)
                    client_tls = TL.client(client_tcp, TL.Config(
                        verify_peer = false,
                        server_name = "localhost",
                    ))
                    displaced_state = TL._native_tls13_state(client_tls)
                    displaced_state.version = TL.TLS1_2_VERSION
                    append!(displaced_state.record_buffer, UInt8[0x71, 0x72])
                    state12 = TL._activate_native_tls12_state!(client_tls, displaced_state)
                    @test client_tls.native_state === state12
                    @test state12.record_buffer == UInt8[0x71, 0x72]
                    @test isempty(displaced_state.record_buffer)

                    key = UInt8[UInt8(0x20 + i) for i in 0:15]
                    iv = UInt8[0xa0, 0xa1, 0xa2, 0xa3]
                    TL._tls12_set_write_cipher!(
                        state12,
                        TL._TLS12_ECDHE_RSA_WITH_AES_128_GCM_SHA256,
                        key,
                        iv,
                    )
                    TL._tls12_set_read_cipher!(
                        peer_state,
                        TL._TLS12_ECDHE_RSA_WITH_AES_128_GCM_SHA256,
                        key,
                        iv,
                    )
                    TL._native_auto_try_write_fatal_alert!(client_tls, TL._TLS_ALERT_BAD_RECORD_MAC)
                    err = try
                        TL._tls12_read_record!(server_tcp, peer_state)
                        nothing
                    catch ex
                        ex
                    end
                    @test err isa TL._TLSAlertError
                    if err isa TL._TLSAlertError
                        @test err.from_peer
                        @test err.alert == TL._TLS_ALERT_BAD_RECORD_MAC
                    end
                    @test (state12.write_cipher::TL._TLS12RecordCipherState).seq == UInt64(1)
                finally
                    TL._securezero_tls12_native_state!(peer_state)
                    _tls_close_quiet!(client_tls)
                    _tls_close_quiet!(server_tcp)
                    _tls_close_quiet!(listener)
                    IP.shutdown!()
                end
            end
        end

        @testset "mixed-version native client negotiates TLS 1.2 with an exact TLS 1.2 server" begin
            IP.shutdown!()
            listener = nothing
            client = nothing
            server = nothing
            try
                listener = TL.listen("tcp", "127.0.0.1:0", TL.Config(
                    verify_peer = false,
                    cert_file = _TLS_CERT_PATH,
                    key_file = _TLS_KEY_PATH,
                    handshake_timeout_ns = 10_000_000_000,
                    min_version = TL.TLS1_2_VERSION,
                    max_version = TL.TLS1_2_VERSION,
                ); backlog = 16)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    try
                        conn = TL.accept(listener)
                        TL.handshake!(conn)
                        write(conn, UInt8[0x31])
                        return conn
                    catch err
                        return err
                    end
                end)
                client = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                    handshake_timeout_ns = 10_000_000_000,
                ))
                @test read(client, 1) == UInt8[0x31]
                _tls_wait_task_done(accept_task)
                server_result = fetch(accept_task)
                server_result isa Exception && throw(server_result)
                server = server_result::TL.Conn
                client_state = TL.connection_state(client)
                server_state = TL.connection_state(server)
                @test client_state.handshake_complete
                @test server_state.handshake_complete
                @test client_state.version == "TLSv1.2"
                @test server_state.version == "TLSv1.2"
                @test !client_state.using_native_tls13
                @test !server_state.using_native_tls13
                @test client_state.curve == "X25519"
                @test server_state.curve == "X25519"
            finally
                _tls_close_quiet!(server)
                _tls_close_quiet!(client)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "mixed-version native server negotiates TLS 1.2 with an exact TLS 1.2 client" begin
            IP.shutdown!()
            listener = nothing
            client = nothing
            server = nothing
            try
                listener = TL.listen("tcp", "127.0.0.1:0", _tls_server_config(handshake_timeout_ns = 10_000_000_000); backlog = 16)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    try
                        conn = TL.accept(listener)
                        TL.handshake!(conn)
                        write(conn, UInt8[0x41])
                        return conn
                    catch err
                        return err
                    end
                end)
                client = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                    handshake_timeout_ns = 10_000_000_000,
                    max_version = TL.TLS1_2_VERSION,
                ))
                @test read(client, 1) == UInt8[0x41]
                _tls_wait_task_done(accept_task)
                server_result = fetch(accept_task)
                server_result isa Exception && throw(server_result)
                server = server_result::TL.Conn
                client_state = TL.connection_state(client)
                server_state = TL.connection_state(server)
                @test client_state.handshake_complete
                @test server_state.handshake_complete
                @test client_state.version == "TLSv1.2"
                @test server_state.version == "TLSv1.2"
                @test !client_state.using_native_tls13
                @test !server_state.using_native_tls13
                @test client_state.curve == "X25519"
                @test server_state.curve == "X25519"
            finally
                _tls_close_quiet!(server)
                _tls_close_quiet!(client)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "mixed-version native TLS can negotiate TLS 1.2 with X25519 when configured" begin
            IP.shutdown!()
            listener = nothing
            client = nothing
            server = nothing
            try
                listener = TL.listen("tcp", "127.0.0.1:0", _tls_server_config(
                    handshake_timeout_ns = 10_000_000_000,
                    curve_preferences = UInt16[TL.X25519],
                ); backlog = 16)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    try
                        conn = TL.accept(listener)
                        TL.handshake!(conn)
                        write(conn, UInt8[0x58])
                        return conn
                    catch err
                        return err
                    end
                end)
                client = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                    handshake_timeout_ns = 10_000_000_000,
                    max_version = TL.TLS1_2_VERSION,
                    curve_preferences = UInt16[TL.X25519],
                ))
                @test read(client, 1) == UInt8[0x58]
                _tls_wait_task_done(accept_task)
                server_result = fetch(accept_task)
                server_result isa Exception && throw(server_result)
                server = server_result::TL.Conn
                client_state = TL.connection_state(client)
                server_state = TL.connection_state(server)
                @test client_state.version == "TLSv1.2"
                @test server_state.version == "TLSv1.2"
                @test !client_state.using_native_tls13
                @test !server_state.using_native_tls13
                @test client_state.curve == "X25519"
                @test server_state.curve == "X25519"
            finally
                _tls_close_quiet!(server)
                _tls_close_quiet!(client)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "mixed-version native client supports TLS 1.2 mTLS and resumption against an exact TLS 1.2 server" begin
            function run_once(server_cfg::TL.Config, client_cfg::TL.Config)
                listener = nothing
                client = nothing
                accept_task = nothing
                try
                    listener = TL.listen("tcp", "127.0.0.1:0", server_cfg; backlog = 16)
                    laddr = TL.addr(listener)::NC.SocketAddrV4
                    accept_task = errormonitor(Threads.@spawn begin
                        conn = TL.accept(listener)
                        try
                            TL.handshake!(conn)
                            write(conn, UInt8[0x51])
                            read(conn, 1) == UInt8[0x61] || error("unexpected TLS client ack")
                            return TL.connection_state(conn)
                        finally
                            _tls_close_quiet!(conn)
                        end
                    end)
                    client = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", client_cfg)
                    read(client, 1) == UInt8[0x51] || error("unexpected TLS server byte")
                    write(client, UInt8[0x61]) == 1 || error("unexpected TLS client ack write")
                    _tls_wait_task_done(accept_task)
                    return TL.connection_state(client), fetch(accept_task)::TL.ConnectionState
                finally
                    _tls_close_quiet!(client)
                    _tls_close_quiet!(listener)
                    IP.shutdown!()
                end
            end

            server_cfg = _tls_server_config(
                handshake_timeout_ns = 10_000_000_000,
                cert_file = _TLS_NATIVE_SERVER_CERT_PATH,
                key_file = _TLS_NATIVE_SERVER_KEY_PATH,
                client_auth = TL.ClientAuthMode.RequireAndVerifyClientCert,
                client_ca_file = _TLS_NATIVE_CA_PATH,
                min_version = TL.TLS1_2_VERSION,
                max_version = TL.TLS1_2_VERSION,
            )
            client_cfg = TL.Config(
                verify_peer = true,
                verify_hostname = true,
                server_name = "localhost",
                ca_file = _TLS_NATIVE_CA_PATH,
                cert_file = _TLS_NATIVE_CLIENT_CERT_PATH,
                key_file = _TLS_NATIVE_CLIENT_KEY_PATH,
                handshake_timeout_ns = 10_000_000_000,
            )

            client_state1, server_state1 = run_once(server_cfg, client_cfg)
            @test client_state1.version == "TLSv1.2"
            @test server_state1.version == "TLSv1.2"
            @test !client_state1.using_native_tls13
            @test !server_state1.using_native_tls13
            @test client_state1.cipher_suite == "TLS_ECDHE_RSA_WITH_AES_128_GCM_SHA256"
            @test server_state1.cipher_suite == "TLS_ECDHE_RSA_WITH_AES_128_GCM_SHA256"
            @test !client_state1.did_resume
            @test !server_state1.did_resume
            @test client_state1.has_resumable_session
            @test client_state1.curve == "X25519"
            @test server_state1.curve == "X25519"

            client_state2, server_state2 = run_once(server_cfg, client_cfg)
            @test client_state2.version == "TLSv1.2"
            @test server_state2.version == "TLSv1.2"
            @test !client_state2.using_native_tls13
            @test !server_state2.using_native_tls13
            @test client_state2.cipher_suite == "TLS_ECDHE_RSA_WITH_AES_128_GCM_SHA256"
            @test server_state2.cipher_suite == "TLS_ECDHE_RSA_WITH_AES_128_GCM_SHA256"
            @test client_state2.did_resume
            @test server_state2.did_resume
            @test client_state2.has_resumable_session
            @test client_state2.curve == "X25519"
            @test server_state2.curve == "X25519"
        end
        @testset "mixed-version native client presents its certificate when TLS 1.3 is negotiated" begin
            function run_once(server_cfg::TL.Config, client_cfg::TL.Config)
                listener = nothing
                client = nothing
                accept_task = nothing
                try
                    listener = TL.listen("tcp", "127.0.0.1:0", server_cfg; backlog = 16)
                    laddr = TL.addr(listener)::NC.SocketAddrV4
                    accept_task = errormonitor(Threads.@spawn begin
                        conn = TL.accept(listener)
                        try
                            TL.handshake!(conn)
                            write(conn, UInt8[0x53])
                            read(conn, 1) == UInt8[0x63] || error("unexpected TLS client ack")
                            return TL.connection_state(conn)
                        finally
                            _tls_close_quiet!(conn)
                        end
                    end)
                    client = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", client_cfg)
                    read(client, 1) == UInt8[0x53] || error("unexpected TLS server byte")
                    write(client, UInt8[0x63]) == 1 || error("unexpected TLS client ack write")
                    _tls_wait_task_done(accept_task)
                    return TL.connection_state(client), fetch(accept_task)::TL.ConnectionState
                finally
                    _tls_close_quiet!(client)
                    _tls_close_quiet!(listener)
                    IP.shutdown!()
                end
            end

            # The client allows both versions (the default), so the mixed-version driver runs.
            client_cfg = TL.Config(
                verify_peer = true,
                verify_hostname = true,
                server_name = "localhost",
                ca_file = _TLS_NATIVE_CA_PATH,
                cert_file = _TLS_NATIVE_CLIENT_CERT_PATH,
                key_file = _TLS_NATIVE_CLIENT_KEY_PATH,
                handshake_timeout_ns = 10_000_000_000,
            )
            for (label, version_kwargs) in (
                ("exact TLS 1.3 server", (; min_version = TL.TLS1_3_VERSION, max_version = TL.TLS1_3_VERSION)),
                ("mixed-version server", (;)),
            )
                @testset "$(label)" begin
                    server_cfg = _tls_server_config(;
                        handshake_timeout_ns = 10_000_000_000,
                        cert_file = _TLS_NATIVE_SERVER_CERT_PATH,
                        key_file = _TLS_NATIVE_SERVER_KEY_PATH,
                        client_auth = TL.ClientAuthMode.RequireAndVerifyClientCert,
                        client_ca_file = _TLS_NATIVE_CA_PATH,
                        version_kwargs...,
                    )
                    client_state1, server_state1 = run_once(server_cfg, client_cfg)
                    @test client_state1.version == "TLSv1.3"
                    @test server_state1.version == "TLSv1.3"
                    @test client_state1.using_native_tls13
                    @test server_state1.using_native_tls13
                    @test !client_state1.did_resume
                    @test !server_state1.did_resume
                    @test client_state1.has_resumable_session
                    # Resumption keeps working with a client identity loaded into the TLS 1.3 state.
                    client_state2, server_state2 = run_once(server_cfg, client_cfg)
                    @test client_state2.version == "TLSv1.3"
                    @test server_state2.version == "TLSv1.3"
                    @test client_state2.did_resume
                    @test server_state2.did_resume
                end
            end
        end
        @testset "mixed-version native server supports TLS 1.2 mTLS and resumption against an exact TLS 1.2 client" begin
            function run_once(server_cfg::TL.Config, client_cfg::TL.Config)
                listener = nothing
                client = nothing
                accept_task = nothing
                try
                    listener = TL.listen("tcp", "127.0.0.1:0", server_cfg; backlog = 16)
                    laddr = TL.addr(listener)::NC.SocketAddrV4
                    accept_task = errormonitor(Threads.@spawn begin
                        conn = TL.accept(listener)
                        try
                            TL.handshake!(conn)
                            write(conn, UInt8[0x52])
                            read(conn, 1) == UInt8[0x62] || error("unexpected TLS client ack")
                            return TL.connection_state(conn)
                        finally
                            _tls_close_quiet!(conn)
                        end
                    end)
                    client = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", client_cfg)
                    read(client, 1) == UInt8[0x52] || error("unexpected TLS server byte")
                    write(client, UInt8[0x62]) == 1 || error("unexpected TLS client ack write")
                    _tls_wait_task_done(accept_task)
                    return TL.connection_state(client), fetch(accept_task)::TL.ConnectionState
                finally
                    _tls_close_quiet!(client)
                    _tls_close_quiet!(listener)
                    IP.shutdown!()
                end
            end

            server_cfg = _tls_server_config(
                handshake_timeout_ns = 10_000_000_000,
                cert_file = _TLS_NATIVE_SERVER_CERT_PATH,
                key_file = _TLS_NATIVE_SERVER_KEY_PATH,
                client_auth = TL.ClientAuthMode.RequireAndVerifyClientCert,
                client_ca_file = _TLS_NATIVE_CA_PATH,
            )
            client_cfg = TL.Config(
                verify_peer = true,
                verify_hostname = true,
                server_name = "localhost",
                ca_file = _TLS_NATIVE_CA_PATH,
                cert_file = _TLS_NATIVE_CLIENT_CERT_PATH,
                key_file = _TLS_NATIVE_CLIENT_KEY_PATH,
                handshake_timeout_ns = 10_000_000_000,
                min_version = TL.TLS1_2_VERSION,
                max_version = TL.TLS1_2_VERSION,
            )

            client_state1, server_state1 = run_once(server_cfg, client_cfg)
            @test client_state1.version == "TLSv1.2"
            @test server_state1.version == "TLSv1.2"
            @test !client_state1.using_native_tls13
            @test !server_state1.using_native_tls13
            @test client_state1.cipher_suite == "TLS_ECDHE_RSA_WITH_AES_128_GCM_SHA256"
            @test server_state1.cipher_suite == "TLS_ECDHE_RSA_WITH_AES_128_GCM_SHA256"
            @test !client_state1.did_resume
            @test !server_state1.did_resume
            @test client_state1.has_resumable_session
            @test client_state1.curve == "X25519"
            @test server_state1.curve == "X25519"

            client_state2, server_state2 = run_once(server_cfg, client_cfg)
            @test client_state2.version == "TLSv1.2"
            @test server_state2.version == "TLSv1.2"
            @test !client_state2.using_native_tls13
            @test !server_state2.using_native_tls13
            @test client_state2.cipher_suite == "TLS_ECDHE_RSA_WITH_AES_128_GCM_SHA256"
            @test server_state2.cipher_suite == "TLS_ECDHE_RSA_WITH_AES_128_GCM_SHA256"
            @test client_state2.did_resume
            @test server_state2.did_resume
            @test client_state2.has_resumable_session
            @test client_state2.curve == "X25519"
            @test server_state2.curve == "X25519"
        end
        @testset "mixed-version native client still offers TLS 1.2 resumption when a TLS 1.3 session is cached" begin
            function run_once(server_cfg::TL.Config, client_cfg::TL.Config)::TL.ConnectionState
                listener = nothing
                client = nothing
                accept_task = nothing
                try
                    listener = TL.listen("tcp", "127.0.0.1:0", server_cfg; backlog = 16)
                    laddr = TL.addr(listener)::NC.SocketAddrV4
                    accept_task = errormonitor(Threads.@spawn begin
                        conn = TL.accept(listener)
                        try
                            TL.handshake!(conn)
                            write(conn, UInt8[0x53])
                            read(conn, 1) == UInt8[0x63] || error("unexpected TLS client ack")
                            return nothing
                        finally
                            _tls_close_quiet!(conn)
                        end
                    end)
                    client = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", client_cfg)
                    read(client, 1) == UInt8[0x53] || error("unexpected TLS server byte")
                    write(client, UInt8[0x63]) == 1 || error("unexpected TLS client ack write")
                    _tls_wait_task_done(accept_task)
                    fetch(accept_task)
                    return TL.connection_state(client)
                finally
                    _tls_close_quiet!(client)
                    _tls_close_quiet!(listener)
                    IP.shutdown!()
                end
            end

            client_cfg = TL.Config(
                verify_peer = false,
                server_name = "localhost",
                handshake_timeout_ns = 10_000_000_000,
            )
            mixed_server_cfg = _tls_server_config(handshake_timeout_ns = 10_000_000_000)
            exact_tls12_server_cfg = _tls_server_config(
                handshake_timeout_ns = 10_000_000_000,
                min_version = TL.TLS1_2_VERSION,
                max_version = TL.TLS1_2_VERSION,
            )

            tls13_state = run_once(mixed_server_cfg, client_cfg)
            @test tls13_state.version == "TLSv1.3"
            @test tls13_state.using_native_tls13
            @test tls13_state.has_resumable_session

            tls12_state1 = run_once(exact_tls12_server_cfg, client_cfg)
            @test tls12_state1.version == "TLSv1.2"
            @test !tls12_state1.using_native_tls13
            @test !tls12_state1.did_resume
            @test tls12_state1.has_resumable_session

            tls12_state2 = run_once(exact_tls12_server_cfg, client_cfg)
            @test tls12_state2.version == "TLSv1.2"
            @test !tls12_state2.using_native_tls13
            @test tls12_state2.did_resume
            @test tls12_state2.has_resumable_session
        end
        @testset "effective TLS 1.2 client routing stays native with client certs" begin
            IP.shutdown!()
            listener = nothing
            client_tls = nothing
            server_tls = nothing
            try
                listener = TL.listen("tcp", "127.0.0.1:0", TL.Config(
                    verify_peer = true,
                    verify_hostname = false,
                    cert_file = _TLS_NATIVE_SERVER_CERT_PATH,
                    key_file = _TLS_NATIVE_SERVER_KEY_PATH,
                    client_auth = TL.ClientAuthMode.RequireAndVerifyClientCert,
                    client_ca_file = _TLS_NATIVE_CA_PATH,
                    min_version = TL.TLS1_2_VERSION,
                    max_version = TL.TLS1_2_VERSION,
                ); backlog = 8)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    conn = TL.accept(listener)
                    TL.handshake!(conn)
                    return conn
                end)
                client_tls = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", TL.Config(
                    verify_peer = true,
                    verify_hostname = true,
                    server_name = "localhost",
                    ca_file = _TLS_NATIVE_CA_PATH,
                    cert_file = _TLS_NATIVE_CLIENT_CERT_PATH,
                    key_file = _TLS_NATIVE_CLIENT_KEY_PATH,
                    min_version = nothing,
                    max_version = TL.TLS1_2_VERSION,
                ))
                @test client_tls.policy == TL._TLS_POLICY_TLS12
                _tls_wait_task_done(accept_task)
                server_tls = fetch(accept_task)
                client_state = TL.connection_state(client_tls)
                server_state = TL.connection_state(server_tls)
                @test client_state.version == "TLSv1.2"
                @test server_state.version == "TLSv1.2"
                @test !client_state.using_native_tls13
                @test !server_state.using_native_tls13
            finally
                _tls_close_quiet!(server_tls)
                _tls_close_quiet!(client_tls)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "write accepts string codeunits buffers" begin
            IP.shutdown!()
            listener = nothing
            client = nothing
            server = nothing
            try
                listener = TL.listen("tcp", "127.0.0.1:0", _tls_server_config(); backlog = 8)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    conn = TL.accept(listener)
                    TL.handshake!(conn)
                    TL.set_read_deadline!(conn, _TLS_FAR_FUTURE_NS)
                    server_codeunits_buf = Vector{UInt8}(undef, 2)
                    read!(conn, server_codeunits_buf)
                    write(conn, server_codeunits_buf)
                    return conn
                end)
                client = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                    handshake_timeout_ns = 10_000_000_000,
                ))
                TL.set_read_deadline!(client, _TLS_FAR_FUTURE_NS)
                @test write(client, codeunits("hi")) == 2
                client_codeunits_buf = Vector{UInt8}(undef, 2)
                @test read!(client, client_codeunits_buf) === client_codeunits_buf
                @test String(client_codeunits_buf) == "hi"
                _tls_wait_task_done(accept_task)
                server = fetch(accept_task)
            finally
                _tls_close_quiet!(server)
                _tls_close_quiet!(client)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "peer read observes clean EOF after close_notify" begin
            IP.shutdown!()
            listener = nothing
            client = nothing
            try
                listener = TL.listen("tcp", "127.0.0.1:0", _tls_server_config(); backlog = 8)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                close_task = errormonitor(Threads.@spawn begin
                    conn = TL.accept(listener)
                    try
                        TL.handshake!(conn)
                    catch
                    end
                    close(conn)
                    return nothing
                end)
                client = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                ))
                _tls_wait_task_done(close_task)
                buf = Vector{UInt8}(undef, 1)
                @test eof(client)
                @test_throws EOFError read!(client, buf)
            finally
                _tls_close_quiet!(client)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "close_write shuts down TLS write side and rejects further writes" begin
            IP.shutdown!()
            listener = nothing
            client = nothing
            server = nothing
            try
                listener = TL.listen("tcp", "127.0.0.1:0", _tls_server_config(); backlog = 8)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    conn = TL.accept(listener)
                    TL.handshake!(conn)
                    return conn
                end)
                client = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                ))
                _tls_wait_task_done(accept_task)
                server = fetch(accept_task)
                closewrite(client)
                write_err = try
                    write(client, UInt8[0x41])
                    nothing
                catch ex
                    ex
                end
                @test write_err isa TL.TLSError
                if write_err isa TL.TLSError
                    @test write_err.message == "tls: protocol is shutdown"
                end
                @test_throws TL.TLSError write(client, UInt8[])
                TL.set_read_deadline!(server, _TLS_FAR_FUTURE_NS)
                @test eof(server)
                @test_throws EOFError read!(server, Vector{UInt8}(undef, 1))
            finally
                _tls_close_quiet!(server)
                _tls_close_quiet!(client)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "close_write before handshake complete returns TLSError" begin
            IP.shutdown!()
            listener = nothing
            client_tcp = nothing
            server_tcp = nothing
            tls_client = nothing
            try
                listener = ND.listen("tcp", "127.0.0.1:0"; backlog = 8)
                laddr = NC.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn NC.accept(listener))
                client_tcp = ND.connect("tcp", "127.0.0.1:$(Int(laddr.port))")
                _tls_wait_task_done(accept_task)
                server_tcp = fetch(accept_task)
                tls_client = TL.client(client_tcp, TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                ))
                err = try
                    closewrite(tls_client)
                    nothing
                catch ex
                    ex
                end
                @test err isa TL.TLSError
                if err isa TL.TLSError
                    @test occursin("before handshake complete", err.message)
                end
            finally
                _tls_close_quiet!(tls_client)
                _tls_close_quiet!(server_tcp)
                _tls_close_quiet!(client_tcp)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "peer verification success with explicit CA file" begin
            IP.shutdown!()
            listener = nothing
            client = nothing
            server = nothing
            try
                listener = TL.listen("tcp", "127.0.0.1:0", _tls_server_config(); backlog = 16)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    try
                        conn = TL.accept(listener)
                        TL.handshake!(conn)
                        return conn
                    catch err
                        return err
                    end
                end)
                client_cfg = TL.Config(
                    verify_peer = true,
                    server_name = "localhost",
                    ca_file = _TLS_CERT_PATH,
                    handshake_timeout_ns = 10_000_000_000,
                )
                connect_result = try
                    _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", client_cfg)
                catch ex
                    ex
                end
                if connect_result isa TL.Conn
                    client = connect_result
                    _tls_wait_task_done(accept_task)
                    server_result = fetch(accept_task)
                    server_result isa Exception && throw(server_result)
                    server = server_result::TL.Conn
                    @test TL.connection_state(client).handshake_complete
                else
                    @test connect_result isa TL.TLSHandshakeTimeoutError
                end
            finally
                _tls_close_quiet!(server)
                _tls_close_quiet!(client)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "hostname verification can be enabled without chain verification" begin
            IP.shutdown!()
            listener = nothing
            client = nothing
            server = nothing
            try
                listener = TL.listen("tcp", "127.0.0.1:0", _tls_server_config(); backlog = 16)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    try
                        conn = TL.accept(listener)
                        TL.handshake!(conn)
                        return conn
                    catch err
                        return err
                    end
                end)
                client_cfg = TL.Config(
                    verify_peer = false,
                    verify_hostname = true,
                    server_name = "localhost",
                    handshake_timeout_ns = 10_000_000_000,
                    max_version = TL.TLS1_2_VERSION,
                )
                client = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", client_cfg)
                _tls_wait_task_done(accept_task)
                server_result = fetch(accept_task)
                server_result isa Exception && throw(server_result)
                server = server_result::TL.Conn
                @test TL.connection_state(client).handshake_complete
                @test !TL.connection_state(client).using_native_tls13
            finally
                _tls_close_quiet!(server)
                _tls_close_quiet!(client)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "hostname verification failure surfaces TLSError without CA verification" begin
            IP.shutdown!()
            listener = nothing
            accept_task = nothing
            try
                listener = TL.listen("tcp", "127.0.0.1:0", _tls_server_config(); backlog = 16)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    conn = TL.accept(listener)
                    try
                        TL.handshake!(conn)
                    catch
                    end
                    _tls_close_quiet!(conn)
                    return nothing
                end)
                client_cfg = TL.Config(
                    verify_peer = false,
                    verify_hostname = true,
                    server_name = "example.com",
                    handshake_timeout_ns = 10_000_000_000,
                    max_version = TL.TLS1_2_VERSION,
                )
                connect_err = try
                    _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", client_cfg)
                    nothing
                catch ex
                    ex
                end
                @test connect_err isa TL.TLSError
                if connect_err isa TL.TLSError
                    @test occursin("certificate is not valid for host", connect_err.message)
                end
                accept_task !== nothing && _tls_wait_task_done(accept_task)
            finally
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "peer verification failure surfaces TLSError" begin
            IP.shutdown!()
            listener = nothing
            accept_task = nothing
            try
                listener = TL.listen("tcp", "127.0.0.1:0", _tls_server_config(); backlog = 16)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    conn = TL.accept(listener)
                    try
                        TL.handshake!(conn)
                    catch
                    end
                    _tls_close_quiet!(conn)
                    return nothing
                end)
                bad_client_cfg = TL.Config(
                    verify_peer = true,
                    server_name = "localhost",
                    handshake_timeout_ns = 10_000_000_000,
                )
                bad_connect_err = try
                    _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", bad_client_cfg)
                    nothing
                catch ex
                    ex
                end
                @test bad_connect_err !== nothing
                if bad_connect_err !== nothing
                    @test _tls_handshake_connect_error(bad_connect_err)
                end
                accept_task !== nothing && _tls_wait_task_done(accept_task)
            finally
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "ip literal verification path infers server_name and succeeds" begin
            IP.shutdown!()
            listener = nothing
            accept_task = nothing
            client = nothing
            server = nothing
            try
                listener = TL.listen("tcp", "127.0.0.1:0", _tls_server_config(); backlog = 16)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    conn = TL.accept(listener)
                    try
                        TL.handshake!(conn)
                        return conn
                    catch
                        _tls_close_quiet!(conn)
                        rethrow()
                    end
                end)
                client_cfg = TL.Config(
                    verify_peer = true,
                    ca_file = _TLS_CERT_PATH,
                    handshake_timeout_ns = 10_000_000_000,
                )
                client = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", client_cfg)
                _tls_wait_task_done(accept_task)
                server = fetch(accept_task)
                client_state = TL.connection_state(client)
                server_state = TL.connection_state(server)
                @test client_state.handshake_complete
                @test server_state.handshake_complete
                @test client_state.version == "TLSv1.3"
                @test server_state.version == "TLSv1.3"
            finally
                _tls_close_quiet!(server)
                _tls_close_quiet!(client)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "handshake timeout surfaces TLSHandshakeTimeoutError" begin
            @test TL._deadline_after_ns(typemax(Int64)) == typemax(Int64)
            IP.shutdown!()
            listener = nothing
            client_tcp = nothing
            stalled_peer = nothing
            accepted = Channel{Nothing}(1)
            release_peer = Channel{Nothing}(1)
            try
                listener = ND.listen("tcp", "127.0.0.1:0"; backlog = 8)
                laddr = NC.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    conn = NC.accept(listener)
                    put!(accepted, nothing)
                    take!(release_peer)
                    return conn
                end)
                client_tcp = ND.connect("tcp", "127.0.0.1:$(Int(laddr.port))")
                take!(accepted)
                client_tls = TL.client(client_tcp, TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                    # 1ns: the handshake deadline is already expired when the
                    # handshake starts, firing before the far-future transport
                    # deadlines below.
                    handshake_timeout_ns = 1,
                ))
                original_read_deadline = _TLS_FAR_FUTURE_NS - Int64(1)
                original_write_deadline = _TLS_FAR_FUTURE_NS - Int64(2)
                TL.set_read_deadline!(client_tls, original_read_deadline)
                TL.set_write_deadline!(client_tls, original_write_deadline)
                pfd = client_tls.tcp.fd.pfd
                pre_read_ns = @atomic :acquire pfd.pd.rd_ns
                pre_write_ns = @atomic :acquire pfd.pd.wd_ns
                @test_throws TL.TLSHandshakeTimeoutError TL.handshake!(client_tls)
                post_read_ns = @atomic :acquire pfd.pd.rd_ns
                post_write_ns = @atomic :acquire pfd.pd.wd_ns
                @test post_read_ns == pre_read_ns
                @test post_write_ns == pre_write_ns
                _tls_close_quiet!(client_tls)
                client_tcp = nothing
                isready(release_peer) || put!(release_peer, nothing)
                _tls_wait_task_done(accept_task)
                stalled_peer = fetch(accept_task)
            finally
                isready(release_peer) || put!(release_peer, nothing)
                _tls_close_quiet!(stalled_peer)
                _tls_close_quiet!(client_tcp)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "earlier transport deadline retains i/o timeout identity" begin
            IP.shutdown!()
            listener = nothing
            client_tcp = nothing
            stalled_peer = nothing
            client_tls = nothing
            accepted = Channel{Nothing}(1)
            release_peer = Channel{Nothing}(1)
            try
                listener = ND.listen("tcp", "127.0.0.1:0"; backlog = 8)
                laddr = NC.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    conn = NC.accept(listener)
                    put!(accepted, nothing)
                    take!(release_peer)
                    return conn
                end)
                client_tcp = ND.connect("tcp", "127.0.0.1:$(Int(laddr.port))")
                take!(accepted)
                client_tls = TL.client(client_tcp, TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                    handshake_timeout_ns = 2_000_000_000,
                ))
                # Expired read deadline fires before the handshake timeout; an
                # already-expired deadline is latched as fired (-1) on arming.
                TL.set_read_deadline!(client_tls, Int64(1))
                TL.set_write_deadline!(client_tls, _TLS_FAR_FUTURE_NS)
                pfd = client_tls.tcp.fd.pfd
                @test (@atomic :acquire pfd.pd.rd_ns) < 0
                pre_write_ns = @atomic :acquire pfd.pd.wd_ns
                err = try
                    TL.handshake!(client_tls)
                    nothing
                catch ex
                    ex
                end
                @test err isa TL.TLSError
                @test !(err isa TL.TLSHandshakeTimeoutError)
                if err isa TL.TLSError
                    @test err.message == "i/o timeout"
                    @test err.cause isa TL.DeadlineExceededError
                end
                @test (@atomic :acquire pfd.pd.rd_ns) < 0
                @test (@atomic :acquire pfd.pd.wd_ns) == pre_write_ns
                _tls_close_quiet!(client_tls)
                client_tls = nothing
                client_tcp = nothing
                isready(release_peer) || put!(release_peer, nothing)
                _tls_wait_task_done(accept_task)
                stalled_peer = fetch(accept_task)
            finally
                isready(release_peer) || put!(release_peer, nothing)
                _tls_close_quiet!(client_tls)
                _tls_close_quiet!(stalled_peer)
                _tls_close_quiet!(client_tcp)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "handshake deadline with no handshake_timeout maps to i/o timeout TLSError" begin
            IP.shutdown!()
            listener = nothing
            client_tcp = nothing
            stalled_peer = nothing
            client_tls = nothing
            accepted = Channel{Nothing}(1)
            release_peer = Channel{Nothing}(1)
            try
                listener = ND.listen("tcp", "127.0.0.1:0"; backlog = 8)
                laddr = NC.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    conn = NC.accept(listener)
                    put!(accepted, nothing)
                    take!(release_peer)
                    return conn
                end)
                client_tcp = ND.connect("tcp", "127.0.0.1:$(Int(laddr.port))")
                take!(accepted)
                client_tls = TL.client(client_tcp, TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                    handshake_timeout_ns = 0,
                ))
                TL.set_read_deadline!(client_tls, Int64(1))
                TL.set_write_deadline!(client_tls, Int64(1))
                err = try
                    TL.handshake!(client_tls)
                    nothing
                catch ex
                    ex
                end
                @test err isa TL.TLSError
                @test !(err isa TL.TLSHandshakeTimeoutError)
                if err isa TL.TLSError
                    @test err.message == "i/o timeout"
                end
                isready(release_peer) || put!(release_peer, nothing)
                _tls_wait_task_done(accept_task)
                stalled_peer = fetch(accept_task)
            finally
                isready(release_peer) || put!(release_peer, nothing)
                _tls_close_quiet!(client_tls)
                _tls_close_quiet!(stalled_peer)
                _tls_close_quiet!(client_tcp)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "host resolver timeout budget includes TLS handshake time" begin
            IP.shutdown!()
            listener = nothing
            accepted = Channel{Nothing}(1)
            release_peer = Channel{Nothing}(1)
            try
                listener = ND.listen("tcp", "127.0.0.1:0"; backlog = 8)
                laddr = NC.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    conn = NC.accept(listener)
                    try
                        put!(accepted, nothing)
                        take!(release_peer)
                        return nothing
                    finally
                        close(conn)
                    end
                end)
                host_resolver = ND.HostResolver(timeout_ns = 250_000_000)
                cfg = TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                    handshake_timeout_ns = 10_000_000_000,
                )
                connect_task = errormonitor(Threads.@spawn begin
                    try
                        _tls_connect(
                            "tcp",
                            "127.0.0.1:$(Int(laddr.port))",
                            cfg;
                            timeout_ns = host_resolver.timeout_ns,
                            deadline_ns = host_resolver.deadline_ns,
                            local_addr = host_resolver.local_addr,
                            fallback_delay_ns = host_resolver.fallback_delay_ns,
                            resolver = host_resolver.resolver,
                            policy = host_resolver.policy,
                        )
                        nothing
                    catch ex
                        ex
                    end
                end)
                take!(accepted)
                _tls_wait_task_done(connect_task)
                err = fetch(connect_task)
                @test !istaskdone(accept_task)
                isready(release_peer) || put!(release_peer, nothing)
                _tls_wait_task_done(accept_task)
                if istaskdone(accept_task)
                    fetch(accept_task)
                end
                @test err isa TL.TLSError
                @test !(err isa TL.TLSHandshakeTimeoutError)
                if err isa TL.TLSError
                    @test err.message == "i/o timeout"
                    @test err.cause isa TL.DeadlineExceededError
                end
            finally
                isready(release_peer) || put!(release_peer, nothing)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "operations fail fast after close" begin
            IP.shutdown!()
            listener = nothing
            client = nothing
            server = nothing
            try
                listener = TL.listen("tcp", "127.0.0.1:0", _tls_server_config(); backlog = 8)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    conn = TL.accept(listener)
                    TL.handshake!(conn)
                    return conn
                end)
                client = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                ))
                _tls_wait_task_done(accept_task)
                server = fetch(accept_task)
                close(client)
                close(client)
                @test_throws TL.TLSError TL.handshake!(client)
                @test_throws TL.TLSError read!(client, Vector{UInt8}(undef, 1))
                @test_throws TL.TLSError write(client, UInt8[0x41])
                @test_throws TL.TLSError read(client, 0)
                @test_throws TL.TLSError read!(client, UInt8[])
                @test_throws TL.TLSError readbytes!(client, UInt8[], 0)
                @test_throws TL.TLSError write(client, UInt8[])
            finally
                _tls_close_quiet!(server)
                _tls_close_quiet!(client)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "readbytes! and read support single-read mode" begin
            IP.shutdown!()
            listener = nothing
            client = nothing
            server = nothing
            try
                listener = TL.listen("tcp", "127.0.0.1:0", _tls_server_config(); backlog = 8)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    conn = TL.accept(listener)
                    TL.handshake!(conn)
                    return conn
                end)
                client = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                ))
                _tls_wait_task_done(accept_task)
                server = fetch(accept_task)

                first_payload = UInt8[0x41, 0x42]
                @test write(client, first_payload) == length(first_payload)
                TL.set_read_deadline!(server, _TLS_FAR_FUTURE_NS)
                first_buf = Vector{UInt8}(undef, 4)
                @test readbytes!(server, first_buf, 4; all = false) == length(first_payload)
                @test first_buf[1:2] == first_payload
                TL.set_read_deadline!(server, Int64(0))

                second_payload = UInt8[0x43, 0x44]
                @test write(client, second_payload) == length(second_payload)
                TL.set_read_deadline!(server, _TLS_FAR_FUTURE_NS)
                @test read(server, 4; all = false) == second_payload
                TL.set_read_deadline!(server, Int64(0))

                third_payload = UInt8[0x45, 0x46]
                @test write(client, third_payload) == length(third_payload)
                TL.set_read_deadline!(server, _TLS_FAR_FUTURE_NS)
                grown_buf = fill(UInt8(0x00), 3)
                @test readbytes!(server, grown_buf, 5; all = false) == length(third_payload)
                @test grown_buf[1:2] == third_payload
                @test length(grown_buf) == 3
                TL.set_read_deadline!(server, Int64(0))

                fourth_payload = UInt8[0x47, 0x48]
                @test write(client, fourth_payload) == length(fourth_payload)
                TL.set_read_deadline!(server, _TLS_FAR_FUTURE_NS)
                view_backing = fill(UInt8(0x00), 5)
                view_buf = @view view_backing[2:4]
                @test readbytes!(server, view_buf, 3; all = false) == length(fourth_payload)
                @test view_backing == UInt8[0x00, 0x47, 0x48, 0x00, 0x00]
                TL.set_read_deadline!(server, Int64(0))
            finally
                _tls_close_quiet!(server)
                _tls_close_quiet!(client)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "write timeout remains sticky across subsequent writes" begin
            IP.shutdown!()
            listener = nothing
            client = nothing
            hold_ready = Channel{Nothing}(1)
            release_hold = Channel{Nothing}(1)
            try
                listener = TL.listen("tcp", "127.0.0.1:0", _tls_server_config(); backlog = 8)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                hold_task = errormonitor(@async begin
                    conn = TL.accept(listener)
                    try
                        TL.handshake!(conn)
                        put!(hold_ready, nothing)
                        take!(release_hold)
                        return nothing
                    finally
                        close(conn)
                    end
                end)
                client = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                ))
                take!(hold_ready)
                # Already expired: any deadline error during a TLS write sets
                # the permanent write error, so no oversized payload or short
                # real deadline is needed.
                TL.set_write_deadline!(client, Int64(1))
                payload = fill(UInt8(0x5a), 1024)
                first_err = try
                    write(client, payload)
                    nothing
                catch ex
                    ex
                end
                @test first_err isa TL.TLSError
                if first_err isa TL.TLSError
                    @test first_err.message == "i/o timeout"
                end
                TL.set_write_deadline!(client, Int64(0))
                second_err = try
                    write(client, UInt8[0x01])
                    nothing
                catch ex
                    ex
                end
                @test second_err isa TL.TLSError
                if first_err isa TL.TLSError && second_err isa TL.TLSError
                    @test second_err === first_err
                end
                isready(release_hold) || put!(release_hold, nothing)
                _tls_wait_task_done(hold_task)
                fetch(hold_task)
            finally
                isready(release_hold) || put!(release_hold, nothing)
                _tls_close_quiet!(client)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
        @testset "blocked read unblocks when local close races" begin
            IP.shutdown!()
            listener = nothing
            client = nothing
            server = nothing
            read_task = nothing
            try
                listener = TL.listen("tcp", "127.0.0.1:0", _tls_server_config(); backlog = 8)
                laddr = TL.addr(listener)::NC.SocketAddrV4
                accept_task = errormonitor(Threads.@spawn begin
                    conn = TL.accept(listener)
                    TL.handshake!(conn)
                    return conn
                end)
                client = _tls_connect("tcp", "127.0.0.1:$(Int(laddr.port))", TL.Config(
                    verify_peer = false,
                    server_name = "localhost",
                ))
                _tls_wait_task_done(accept_task)
                server = fetch(accept_task)
                read_task = errormonitor(Threads.@spawn begin
                    try
                        read!(client, Vector{UInt8}(undef, 1))
                    catch err
                        return err
                    end
                    return :ok
                end)
                # The server never writes, so a completed read here means a
                # spurious wake or error already happened.
                @test !istaskdone(read_task)
                close(client)
                _tls_wait_task_done(read_task)
                result = fetch(read_task)
                @test result isa TL.TLSError
                if result isa TL.TLSError
                    @test occursin("connection is closed", result.message)
                end
            finally
                _tls_close_quiet!(server)
                _tls_close_quiet!(client)
                _tls_close_quiet!(listener)
                IP.shutdown!()
            end
        end
    end
