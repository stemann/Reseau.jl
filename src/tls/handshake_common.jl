# Small helpers shared by multiple handshake state machines.
#
# The version-specific files own the handshake flights and transcript logic;
# this file only holds policy decisions that are identical across those flows.

function _tls_select_server_alpn(config, client_hello::_ClientHelloMsg)::String
    isempty(config.alpn_protocols) && return ""
    isempty(client_hello.alpn_protocols) && return ""
    http11_fallback = false
    for proto in config.alpn_protocols
        for client_proto in client_hello.alpn_protocols
            proto == client_proto && return proto
            proto == "h2" && client_proto == "http/1.1" && (http11_fallback = true)
        end
    end
    http11_fallback && return ""
    _tls_fail(_TLS_ALERT_NO_APPLICATION_PROTOCOL, "tls: client and server do not support a common ALPN protocol")
end

@inline _tls_should_request_client_certificate(config)::Bool =
    config.client_auth != ClientAuthMode.NoClientCert

# A false result means the peer signed with a scheme its certified key cannot
# have produced. Callers report that like any other failed signature check
# (decrypt_error), matching Go's verifyHandshakeSignature.
@inline function _tls13_signature_scheme_matches_public_key(
    signature_algorithm::UInt16,
    public_key::_TLSPublicKey,
)::Bool
    if public_key isa _TLSRSAPublicKey
        return signature_algorithm == _TLS_SIGNATURE_RSA_PSS_RSAE_SHA256 ||
            signature_algorithm == _TLS_SIGNATURE_RSA_PSS_RSAE_SHA384 ||
            signature_algorithm == _TLS_SIGNATURE_RSA_PSS_RSAE_SHA512 ||
            signature_algorithm == _TLS_SIGNATURE_RSA_PSS_PSS_SHA256 ||
            signature_algorithm == _TLS_SIGNATURE_RSA_PSS_PSS_SHA384 ||
            signature_algorithm == _TLS_SIGNATURE_RSA_PSS_PSS_SHA512
    end
    if public_key isa _TLSECPublicKey
        curve_id = (public_key::_TLSECPublicKey).curve_id
        return (curve_id == _TLS_GROUP_SECP256R1 && signature_algorithm == _TLS_SIGNATURE_ECDSA_SECP256R1_SHA256) ||
            (curve_id == _TLS_GROUP_SECP384R1 && signature_algorithm == _TLS_SIGNATURE_ECDSA_SECP384R1_SHA384) ||
            (curve_id == _TLS_GROUP_SECP521R1 && signature_algorithm == _TLS_SIGNATURE_ECDSA_SECP521R1_SHA512)
    end
    return public_key isa _TLSEd25519PublicKey && signature_algorithm == _TLS_SIGNATURE_ED25519
end

# Used when deciding whether a cached resumption session is still valid for the
# current server-side client-auth policy. Resumption should not bypass a stricter
# certificate requirement than the original session satisfied.
function _tls_server_session_client_auth_ok(
    verify_chain!::F,
    client_certificates::Vector{Vector{UInt8}},
    config,
)::Bool where {F}
    mode = config.client_auth
    has_client_certificates = !isempty(client_certificates)
    if mode == ClientAuthMode.NoClientCert
        return !has_client_certificates
    end
    if mode == ClientAuthMode.RequireAnyClientCert || mode == ClientAuthMode.RequireAndVerifyClientCert
        has_client_certificates || return false
    end
    has_client_certificates || return true
    if mode == ClientAuthMode.VerifyClientCertIfGiven || mode == ClientAuthMode.RequireAndVerifyClientCert
        try
            verify_chain!(client_certificates)
            return true
        catch
            return false
        end
    end
    return true
end

# The acceptable-CA half of Go's CertificateRequestInfo.SupportsCertificate: an
# empty certificate_authorities list accepts any chain, otherwise some certificate
# in the chain must have been issued by one of the named CAs. Unlike Go, a local
# certificate our parser cannot read raises instead of silently withholding the
# identity: that is a misconfiguration, not a negotiation outcome.
function _tls_chain_signed_by_acceptable_ca(
    certificate_chain::Vector{Vector{UInt8}},
    acceptable_cas::Vector{Vector{UInt8}},
)::Bool
    isempty(acceptable_cas) && return true
    for cert_der in certificate_chain
        issuer_raw = _tls_parse_der_certificate_info(cert_der).issuer_raw
        for ca in acceptable_cas
            issuer_raw == ca && return true
        end
    end
    return false
end
